#lang ivy1.7

################################################################################
#
# Leader election in a ring example
#
################################################################################


################################################################################
#
# A module for axiomatizing a total order
#
################################################################################

module total_order(r) = {
    axiom r(X,X)                        # Reflexivity
    axiom r(X, Y) & r(Y, Z) -> r(X, Z)  # Transitivity
    axiom r(X, Y) & r(Y, X) -> X = Y    # Anti-symmetry
    axiom r(X, Y) | r(Y, X)             # Totality
}


################################################################################
#
# Module describing a ring topology.
#
# The module includes an anti-reflexive ternary btw relation.
#
# The module also includes get_next and get_prev actions.
#
# In this module, the ring topology is arbitrary and fixed.
#
################################################################################

module ring_topology(carrier) = {

    relation btw(X:carrier,Y:carrier, Z:carrier) # Y is on the acyclic path from X to Z

    # Axiom defining the btw relation - note it's not reflexive
    # not needed: axiom btw(X,Y,Z) -> X ~= Y & X ~= Z & Y ~= Z  # anti-reflexive
    axiom btw(W,X,Y) & btw(W,Y,Z) -> btw(W,X,Z)  # transitive
    axiom btw(W,X,Y) -> ~btw(W,Y,X)  # acyclic
    axiom btw(W,X,Y) | btw(W,Y,X) | W=X | W=Y | X=Y  # total
    axiom btw(X,Y,Z) -> btw(Y,Z,X)  # cyclic permutations

    action get_next(x:carrier) returns (y:carrier) = {
        assume x ~= y & ((Z ~= x & Z ~= y) -> btw(x,y,Z))
    }

}


################################################################################
#
# Types, relations and functions describing state of the network
#
################################################################################

type node
type id

# A ring topology of nodes
instantiate ring : ring_topology(node)

# A total order on ids
relation le(X:id, Y:id)
instantiate total_order(le)

# A function relating a node to its id
individual idn(X:node) : id
axiom forall X,Y. idn(X) = idn(Y) -> X = Y  # the idn function is injective

axiom forall I:id. exists N:node. idn(N) = I

# A relation that keeps track of nodes that think they are the leader
relation leader(N:node)

# A relation for pending messages, a message is just an id
relation pending(V:id, N:node) # The identity V is pending at node N

################################################################################
#
# Protocol description
#
# Two action: send and receive
#
################################################################################

after init {
    leader(N) := false;
    pending(V,N) := false;
}

action send(n:node) = {
    # send my own id to the next node
    var s := ring.get_next(n);
    pending(idn(n), s) := true;
}

action receive(n:node, m:id) = {
    # receive a message from the right neighbor
    assume pending(m, n);
    pending(m, n) := *; # abstract the number of pending messages
    if m = idn(n) {  # Found a leader
        leader(n) := true;
    } else {
        if le(idn(n), m) { # pass message to next node
            var s := ring.get_next(n);
            pending(m, s) := true;
        } # otherwise drop the message...
    }
}

export send
export receive

# invariant forall I:id. exists N:node. idn(N) = I

# The safety property:
invariant [safety] forall X: node, Y:node. leader(X) & leader(Y) -> X = Y  # at most one leader

# invariants obtained via CTI's
invariant [max_id] forall N1,N2. leader(N1) -> le(idn(N2),idn(N1)) # The leader has the higest id
invariant [self_pending] forall N1,N2. pending(idn(N1),N1) -> le(idn(N2),idn(N1)) # Only the highest id is self-pending
invariant [bypass] forall N1,N2,N3. ring.btw(N1,N2,N3) & pending(idn(N2),N1) -> le(idn(N3),idn(N2)) #Cannot bypass nodes with higher ID
